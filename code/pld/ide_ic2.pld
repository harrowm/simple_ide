Name      ide_ic2v3;
Partno    ;
Date      9/02/25;
Revision  03;
Designer  Malcolm Harrow;
Company   Malcolm Harrow, MIT License;
Assembly  None;
Location  None;
Device    g22V10;

/****************************************************************/
/*                                                              */
/* PLD code for the simple ide interface, sets CS and R/W lines */
/*                                                              */
/****************************************************************/

/* Pin Map
        --------
CPUCLK |1     24| VCC
AS     |2     23| IDECS1
SELECT |3     22| IDECS0
A4     |4     21| IDEWR
A3     |5     20| IDERD
A2     |6     19| IDEBUF
A1     |7     18| DTACK
LDS    |8     17| DTACK_ENABLE
UDS    |9     16| WAIT
RW     |10    15| VPA_ENABLE
RESET  |11    14| VPA
GND    |12    13| CPUSP
        --------
*/

/**  Inputs  **/
PIN 1           = CPUCLK;
PIN 2           = AS;           
PIN 3           = SELECT;
PIN [4..7]      = [A4..1];
PIN 8           = LDS;
PIN 9           = UDS;
PIN 10          = RW;
PIN 11          = RESET;
PIN 13          = CPUSP;

/**  Outputs  **/
PIN 14          = VPA;
PIN 15          = VPA_ENABLE;
PIN 16          = WAIT;
PIN 17          = DTACK_ENABLE;
PIN 18          = DTACK;
PIN 19          = IDEBUF;
PIN 20          = IDERD;
PIN 21          = IDEWR;
PIN 22          = IDECS0;
PIN 23          = IDECS1; 

/** Logic Equations **/
WAIT.sp         = 'b'0; 
WAITTWO.sp      = 'b'0; 
DTACK_ENABLE.sp = 'b'0; 

!IDECS0         = SELECT & !A4;	
!IDECS1         = SELECT & A4;	

DTACK.sp        = 'b'0;  /* No need to use IDERDY as always 0 */
DTACK.oe        = DTACK_ENABLE; 

IDEBUF          = !SELECT;

ds              = !UDS # !LDS; /* Not sure I need LDS .. */

!IDERD          =  RW & SELECT & ds & WAIT;
!IDEWR          = !RW & SELECT & ds;

VPA             = 'b'1;
VPA.oe          = VPA_ENABLE;
VPA_ENABLE      = CPUSP & !A3 & A2 & A1;

FIELD STATE = [WAIT, DTACK, DTACK_ENABLE];

SEQUENCE STATE {
    PRESENT 'b'000
        IF SELECT & ds & RW 
            NEXT 'b'100;
        IF SELECT & ds & !RW
            NEXT 'b'001;
        DEFAULT NEXT 'b'000;
    /* READ Sequence */
    PRESENT 'b'100
        NEXT 'b'101;
    PRESENT 'b'101
        IF !ds
            NEXT 'b'111;
        DEFAULT NEXT 'b'101;
    PRESENT 'b'111
        NEXT 'b'110;
    PRESENT 'b'110
        NEXT 'b'000;
    /* WRITE Sequence */
    PRESENT 'b'001
        IF !ds
            NEXT 'b'011;
        DEFAULT NEXT 'b'001;
    PRESENT 'b'011
        NEXT 'b'010;
    PRESENT 'b'010
        NEXT 'b'000;
}
